module summary_m

   use globvar, only: ntotal, nvirt, nghos, niac, pairs, parts, maxtimestep, print_step, save_step, cputime, output_time, &
                      itimestep, time, niac, niac_d

   public:: time_print, preamble, print_summary, print_update

contains

   !==============================================================================================================================
   subroutine time_print
      !***************************************************************************************************
      !   TIME_PRINT:      Print out the current date and time.
      !   The standard Fortran 90 routine DATE_AND_TIME is used to get the current
      !   date and time strings.
      !***************************************************************************************************
      implicit none
      integer, parameter :: output = 6

      !Local scalars.
      character(len=8) :: datstr
      character(len=10) :: timstr

      !Get the current date and time.
      call date_and_time(datstr, timstr)

      !Write out the date and time.
      write (output, "(/31x,A)") "Date = "//datstr(7:8)//"/"//datstr(5:6)//"/"//datstr(1:4)
      write (output, "(30x,A)") "Time = "//timstr(1:2)//":"//timstr(3:4)//":"//timstr(5:10)

   end subroutine time_print

   !==================================================================================================================================
   subroutine preamble

      implicit none
      character(len=100):: args(3)
      integer:: ierr

      if (command_argument_count() .lt. 3) then
         write (*, '(A79)') '!!!ERROR!!! --- 3 commandline arguments must be provided: maxtimestep,'
         write (*, '(A56)') '                print_step, save_step. Program ending...'
         stop
      else
         call get_command_argument(1, args(1))
         call get_command_argument(2, args(2))
         call get_command_argument(3, args(3))
         read (args, *) maxtimestep, print_step, save_step
      end if

      call time_print

      write (*, '(A36)') 'Parallel code - offloading to GPU...'

      write (*, '(A8,I7,A9)') 'Running ', maxtimestep, ' step(s).'
      write (*, '(A33,I7,A9)') 'Printing summary to screen every ', print_step, ' step(s).'
      write (*, '(A29,I7,A9)') 'Writing output to disc every ', save_step, ' step(s).'

   end subroutine preamble

   !==================================================================================================================================
   subroutine print_summary

      implicit none
      integer:: i, j, k, d

      write (*, '(A79)') '================================= TIME SUMMARY ================================'
      write (*, '(A29,F14.7)') 'Average Total CPU time (s) = ', cputime
      write (*, '(A29,F14.7)') 'Average Output time (s) =    ', output_time

   end subroutine print_summary

   !==================================================================================================================================
   subroutine print_update

      implicit none
      integer:: maxp, minp, sumiac, maxiac, miniac, noiac, i, j, k, d

      write (*, '(A79)') '_______________________________________________________________________________'
      write (*, '(A22,I7,A1,I7,9x,A19,F14.7)') '  current time step = ', itimestep, '/', maxtimestep, &
         'current time (s) = ', real(time)
      write (*, '(A65,F14.7)') '                                                  Walltime (s) = ', real(cputime)
      write (*, '(A79)') '_______________________________________________________________________________'

      !Statistics for the interaction, Print information to screen
      niac = niac_d
      sumiac = 0
      maxiac = -huge(1)
      noiac = 0
      miniac = huge(1)
      maxp = 1
      do i = 1, ntotal + nvirt + nghos
         sumiac = sumiac + niac(i)
         if (niac(i) > maxiac) then
            maxiac = niac(i)
            maxp = i
         end if
         if (niac(i) < miniac) then
            miniac = niac(i)
            minp = i
         end if
         if (niac(i) .eq. 0) noiac = noiac + 1
      end do
      write (*, '(A40,I7)') ' >> Statistics: no. of ghost particles: ', nghos
      write (*, '(A42)') ' >> Statistics: interactions per particle:'
      write (*, '(A14,I7,A23,I4)') '    Particle: ', maxp, ' maximal interactions: ', maxiac
      write (*, '(A14,I7,A23,I4)') '    Particle: ', minp, ' minimal interactions: ', miniac
      write (*, '(A14,F14.7)') '    Average : ', real(sumiac)/real(ntotal + nvirt + nghos)
      write (*, '(A18,I9)') '    Total pairs : ', sumiac
      write (*, '(A36,I7)') '    Particles with no interactions: ', noiac

   end subroutine print_update

end module summary_m
