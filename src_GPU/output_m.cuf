module output_m

   use datatypes, only: particles
   use param, only: output_directory, f, dim

   use hdf5
   use h5lt

   private
   public:: output

contains

   !==============================================================================================================================
   subroutine output(itimestep, save_step, ntotal, nvirt, itype, ind, p, rho, x, vx)
      ! Subroutine to write data to disk. Called intermittently, determined by save_step supplied at run-time

      implicit none
      integer, intent(in):: itimestep, save_step, ntotal, nvirt, itype(ntotal+nvirt), ind(ntotal+nvirt)
      real(f), intent(in):: p(ntotal+nvirt), rho(ntotal+nvirt), x(dim,ntotal+nvirt), vx(dim,ntotal+nvirt)
      integer(HID_T):: file_id, real_group_id, virt_group_id
      character(len=4)::number
      integer:: n, ierr, i
      type(particles), allocatable:: ptmp(:)

      n = itimestep/save_step
      write (number, '(I4.4)') n

      ! Initializing hdf5 interface
      call h5open_f(ierr)

      ! Creating output file
      call h5fcreate_f(trim(output_directory)//"/sph_out"//number//".h5", h5F_ACC_TRUNC_F, file_id, ierr)

      ! Creating groups for each of real, virtual, and ghost particles
      call h5gcreate_f(file_id, "real", real_group_id, ierr)
      call h5gclose_f(real_group_id, ierr)

      call h5gcreate_f(file_id, "virt", virt_group_id, ierr)
      call h5gclose_f(virt_group_id, ierr)
      
      allocate(ptmp(max(ntotal,nvirt)))
      
      n = 0
      do i = 1,ntotal+nvirt
         if (itype(i)==1) then
            n = n + 1
            ptmp(n)%itype = itype(i)
            ptmp(n)%indglob = ind(i)
            ptmp(n)%p = p(i)
            ptmp(n)%rho = rho(i)
            ptmp(n)%x = x(:,i)
            ptmp(n)%vx = vx(:,i)
         end if
      end do
      
      call write_particle_data(file_id, 'real', ptmp(1:n))
      
      n = 0
      do i = 1,ntotal+nvirt
         if (itype(i) < 0) then
            n = n + 1
            ptmp(n)%itype = itype(i)
            ptmp(n)%indglob = ind(i)
            ptmp(n)%p = p(i)
            ptmp(n)%rho = rho(i)
            ptmp(n)%x = x(:,i)
            ptmp(n)%vx = vx(:,i)
         end if
      end do
      call write_particle_data(file_id, 'virt', ptmp(1:n))

      call h5fclose_f(file_id, ierr)

   end subroutine output

   !===============================================================================================================================
   subroutine write_particle_data(fid_in, group_label, pts_in)

      implicit none
      integer(HID_T), intent(in):: fid_in
      character(*), intent(in):: group_label
      type(particles), intent(in):: pts_in(:)
      integer(HSIZE_T):: data_dims(2)
      integer:: nelem, i, ierr
      integer, allocatable:: idatatmp(:, :)
      real(f), allocatable:: fdatatmp(:, :)

      nelem = size(pts_in)

      ! Int data
      data_dims(:) = [nelem, 1]
      allocate (idatatmp(data_dims(1), data_dims(2)))

      ! particle index
      idatatmp(:, 1) = pts_in(:)%indglob
      call H5LTmake_dataset_int_f(fid_in, group_label//'/ind', 1, data_dims, idatatmp, ierr)

      ! process ID
      idatatmp(:, 1) = 0
      call H5LTmake_dataset_int_f(fid_in, group_label//'/procid', 1, data_dims, idatatmp, ierr)

      ! particle type
      idatatmp(:, 1) = pts_in(:)%itype
      call H5LTmake_dataset_int_f(fid_in, group_label//'/type', 1, data_dims, idatatmp, ierr)
      deallocate (idatatmp)

      ! float data
      data_dims(:) = [dim, nelem]
      allocate (fdatatmp(data_dims(1), data_dims(2)))

      ! position
      do i = 1, nelem
         fdatatmp(:, i) = pts_in(i)%x(:)
      end do
      select case (f)
      case (kind(1.))
         call H5LTmake_dataset_float_f(fid_in, group_label//'/x', 2, data_dims, fdatatmp, ierr)
      case default
         call H5LTmake_dataset_double_f(fid_in, group_label//'/x', 2, data_dims, fdatatmp, ierr)
      end select

      ! velocity
      do i = 1, nelem
         fdatatmp(:, i) = pts_in(i)%vx(:)
      end do
      select case (f)
      case (kind(1.))
         call H5LTmake_dataset_float_f(fid_in, group_label//'/v', 2, data_dims, fdatatmp, ierr)
      case default
         call H5LTmake_dataset_double_f(fid_in, group_label//'/v', 2, data_dims, fdatatmp, ierr)
      end select

      deallocate (fdatatmp)

      ! density
      data_dims(:) = [nelem, 1]
      allocate (fdatatmp(data_dims(1), data_dims(2)))
      fdatatmp(:, 1) = pts_in(:)%rho
      select case (f)
      case (kind(1.))
         call H5LTmake_dataset_float_f(fid_in, group_label//'/rho', 1, data_dims, fdatatmp, ierr)
      case default
         call H5LTmake_dataset_double_f(fid_in, group_label//'/rho', 1, data_dims, fdatatmp, ierr)
      end select

      ! pressure
      fdatatmp(:, 1) = pts_in(:)%p
      select case (f)
      case (kind(1.))
         call H5LTmake_dataset_float_f(fid_in, group_label//'/p', 1, data_dims, fdatatmp, ierr)
      case default
         call H5LTmake_dataset_double_f(fid_in, group_label//'/p', 1, data_dims, fdatatmp, ierr)
      end select

      deallocate (fdatatmp)

   end subroutine write_particle_data

end module output_m
