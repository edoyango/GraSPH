module time_integration_m

   use datatypes, only: particles, interactions
   use param, only: f, dim, rh0, gamma, c, dt, mass, g, irho, hsml

   use cudafor

   use input_m, only: vmat_update, update_ghost_part, generate_ghost_part
   use flink_list_m, only: flink_list
   use output_m, only: output
   use summary_m, only: print_update
   use single_step_m, only: single_step_kernel

   private
   public:: time_integration

contains

   !==============================================================================================================================
   subroutine time_integration(scale_k, print_step, save_step, maxtimestep, cputime, output_time, test_time, maxinter_perp, maxn, &
                               ntotal, nvirt, nghos, parts, parts_d, niac, niac_d, pairs, pairs_d)
      ! Subroutine responsible for the main time-integration loop

      implicit none
      real(f), intent(in):: scale_k
      integer, intent(in):: print_step, save_step, maxtimestep, maxinter_perp, maxn, ntotal, nvirt
      integer, intent(inout):: nghos
      real(f), intent(out):: cputime, output_time, test_time
      integer, intent(out):: niac(:)
      integer, device, intent(out):: niac_d(:)
      type(particles), intent(inout):: parts(:)
      type(particles), device, intent(out):: parts_d(:)
      type(interactions), intent(out):: pairs(:, :)
      type(interactions), device, intent(out):: pairs_d(:, :)
      integer:: i, j, k, d, n, istat, ki, itimestep
      integer, device:: ntotal_d, nvirt_d, nghos_d
      real(f), allocatable, device:: v_min(:, :), rho_min(:), dvxdt(:, :, :), drho(:, :)
      real(f):: time
      real:: tmpTime
      type(cudaEvent):: startEvent, stopEvent, startOutputEvent, stopOutputEvent, startTestEvent, stopTestEvent
      integer, device, allocatable:: gind(:)

      ! Initializing timings
      cputime = 0._f; output_time = 0._f; test_time = 0._f

      ! creating cuda events and recording start event
      istat = cudaEventCreate(startEvent)
      istat = cudaEventCreate(stopEvent)
      istat = cudaEventCreate(startOutputEvent)
      istat = cudaEventCreate(stopOutputEvent)
      istat = cudaEventCreate(startTestEvent)
      istat = cudaEventCreate(stopTestEvent)
      istat = cudaEventRecord(startEvent, 0)

      ! Allocating arrays relevant to solver
      allocate (v_min(dim, ntotal), rho_min(ntotal), dvxdt(dim, maxn, 4), drho(maxn, 4))
      allocate (gind(ntotal))

      ! Transferring necessary data to GPU
      parts_d(:) = parts(:)
      ntotal_d = ntotal
      nvirt_d = nvirt
      nghos_d = 0

      ! Time-integration (Leap-Frog)
      time = 0._f
      do itimestep = 1, maxtimestep

         ! Save particles' velocity, density information at start of time-step
         !$cuf kernel do <<<*,*>>>
         do i = 1, ntotal
            rho_min(i) = parts_d(i)%rho
            v_min(:, i) = parts_d(i)%vx(:)
            parts_d(i)%p = rh0*c**2*((parts_d(i)%rho/rh0)**gamma - 1._f)/gamma
         end do

         nghos_d = 0
         call generate_ghost_part <<< ntotal/128 + 1, 128 >>> (scale_k, ntotal_d, nvirt_d, nghos_d, parts_d, gind)
         nghos = nghos_d

         !Interaction parameters, calculating neighboring particles
         call flink_list(scale_k, maxinter_perp, ntotal, nvirt, nghos, ntotal_d, nvirt_d, nghos_d, parts_d, niac_d, pairs_d)

         !$cuf kernel do (2) <<<(*,*),(*,*)>>>
         do ki = 1, 4
            do i = 1, ntotal
               dvxdt(1:dim - 1, i, ki) = 0._f
               dvxdt(dim, i, ki) = -g
               drho(i, ki) = 0._f
            end do
         end do

         ! Beginning RK4 update (4 updates per timestep)
         do ki = 1, 4

            ! launching GPU kernel to calculate accelerations and density changes
            call single_step_kernel <<< (ntotal + nvirt + nghos)/32 + 1, 32 >>> (ki, maxinter_perp, ntotal_d, nvirt_d, nghos_d, &
                                                                                 niac_d, pairs_d, parts_d, dvxdt(:, :, ki), &
                                                                                 drho(:, ki))
            ! launching GPU kernel to update particles' speed and density
            call RK4_update <<< ntotal/1024 + 1, 1024 >>> (ki, ntotal_d, v_min, rho_min, dvxdt, drho, parts_d)
            call update_ghost_part <<< nghos/1024 + 1, 1024 >>> (ntotal_d, nvirt_d, nghos_d, parts_d, gind)
         end do

         ! Update time
         time = time + dt

         ! write output data
         if (mod(itimestep, save_step) .eq. 0) then
            ! start timing of write time
            istat = cudaEventRecord(startOutputEvent, 0)
            ! transfer particle data from GPU to CPU
            parts = parts_d
            nghos = nghos_d
            call output(itimestep, save_step, ntotal, nvirt, nghos, parts)
            ! End timing of output time
            istat = cudaEventRecord(stopOutputEvent, 0)
            istat = cudaEventSynchronize(stopOutputEvent)
            istat = cudaEventElapsedTime(tmpTime, startOutputEvent, stopOutputEvent)
            output_time = output_time + tmpTime/1000.
         end if

         if (mod(itimestep, print_step) .eq. 0) then
            istat = cudaEventRecord(stopEvent, 0)
            istat = cudaEventSynchronize(stopEvent)
            istat = cudaEventElapsedTime(cputime, startEvent, stopEvent)
            istat = cudaEventRecord(startOutputEvent, 0)
            niac(:) = niac_d(:)
            nghos = nghos_d
            cputime = cputime/1000.
            call print_update(itimestep, maxtimestep, cputime, time, ntotal, nvirt, nghos, niac)
            istat = cudaEventRecord(stopOutputEvent, 0)
            istat = cudaEventSynchronize(stopOutputEvent)
            istat = cudaEventElapsedTime(tmpTime, startOutputEvent, stopOutputEvent)
            output_time = output_time + tmpTime/1000.
         end if

      end do

      ! Ending timing of compute
      istat = cudaEventRecord(stopEvent, 0)
      istat = cudaEventSynchronize(stopEvent)
      istat = cudaEventElapsedTime(cputime, startEvent, stopEvent)
      cputime = cputime/1000.

   end subroutine time_integration

   !===============================================================================================================================
   attributes(global) pure subroutine RK4_update(ki, nt, v_min, rho_min, dvxdt, drhodt, pts)

      implicit none
      integer, intent(in), value:: ki
      integer, intent(in):: nt
      real(f), intent(in):: v_min(dim, nt), rho_min(nt)
      real(f), intent(in):: dvxdt(:, :, :), drhodt(:, :)
      type(particles), intent(inout):: pts(nt)
      integer:: i

      i = blockDim%x*(blockIdx%x - 1) + threadIdx%x

      if (i <= nt) then
         select case (ki)
         case default
            pts(i)%vx(:) = v_min(:, i) + 0.5_f*dt*dvxdt(:, i, ki)
            pts(i)%rho = rho_min(i) + 0.5_f*dt*drhodt(i, ki)
         case (3)
            pts(i)%vx(:) = v_min(:, i) + dt*dvxdt(:, i, 3)
            pts(i)%rho = rho_min(i) + dt*drhodt(i, 3)
         case (4)
            pts(i)%vx(:) = v_min(:, i) + &
                           dt/6_f*(dvxdt(:, i, 1) + 2._f*dvxdt(:, i, 2) + 2._f*dvxdt(:, i, 3) + dvxdt(:, i, 4))
            pts(i)%rho = rho_min(i) + dt/6_f*(drhodt(i, 1) + 2._f*drhodt(i, 2) + 2._f*drhodt(i, 3) + drhodt(i, 4))
            pts(i)%x(:) = pts(i)%x(:) + dt*pts(i)%vx(:)
         end select
         pts(i)%p = rh0*c**2*((pts(i)%rho/rh0)**gamma - 1._f)/gamma

      end if

   end subroutine RK4_update

end module time_integration_m
