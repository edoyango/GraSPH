program SPH

   use datatypes, only: particles, interactions, time_tracking
   use param, only: skf, f

   use input_m, only: return_ntotal, return_nvirt, read_input_and_allocate
   use flink_list_m, only: kernel_k
   use output_m, only: output
   use summary_m, only: preamble, time_print, print_summary
   use time_integration_m, only: time_integration

   implicit none
   integer:: maxn, ntotal, nvirt, print_step, save_step, maxtimestep, maxinter
   real(f):: scale_k
   integer:: niac
   integer, allocatable:: itype(:), ind(:)
   real(f), allocatable:: p(:), rho(:), x(:,:), vx(:,:)
   type(interactions), allocatable:: pairs(:)
   type(time_tracking):: timings
   integer, parameter:: maxinter_perp = 120

   !Printing preamble to screen
   call preamble(maxtimestep, print_step, save_step)

   ! setting k parameter for kernel radius (r = kh)
   scale_k = kernel_k(skf)

   ntotal = return_ntotal()
   nvirt = return_nvirt()

   write (*, '(A24,1x,I9,1x,A19)') 'Total simulation size of', ntotal, 'physical particles.'
   write (*, '(A24,1x,I9,1x,A19)') '                        ', nvirt, 'virtual particles.'

   !Creat physical and virtual boundary particles
   call read_input_and_allocate(ntotal, nvirt, maxinter_perp, itype, ind, p, rho, x, vx, pairs, maxn, maxinter)
   call output(0, save_step, ntotal, nvirt, itype, ind, p, rho, x, vx)

   !Entering discretized time-integration loop
   call time_integration(scale_k, print_step, save_step, maxtimestep, timings, maxinter, maxn, ntotal, nvirt, niac, &
   pairs, itype, ind, p, rho, x, vx)

   !Printing post-amble to terminal
   call time_print
   call print_summary(timings)

end
